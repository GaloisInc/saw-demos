// LLVM utility definitions

// Remove 'crucible' from common commands
let points_to = crucible_points_to;
let precond = crucible_precond;
let from_cryptol = crucible_term;

let execute = crucible_execute_func;
let postcond = crucible_postcond;
let returns = crucible_return;
let array = crucible_array;
let llvm_verify module function_name overrides spec =
  crucible_llvm_verify module function_name overrides true spec z3;

// Declare constants used to denote whether an allocation is
// 'READONLY' (CONST) or not (WRITE).
let CONST = {{ False }};
let WRITE = {{ True }};

// Returns a newly-allocated pointer of the given type.
let pointer (rw : Term) (type : LLVMType) = if (eval_bool {{ rw == WRITE }})
                      then crucible_alloc type
                      else crucible_alloc_readonly type;

//Symbolic value --- for use with the `variable` function.
let FRESH   = { fresh={{ True }} : Term, t={{ error "NEW Term field was accessed" }} : Term};
//Concrete value --- for use with the `variable` function.
let STALE (t : Term) = { fresh={{ False }} : Term, t=t : Term};

let variable_empty = do {
  return {{ False }};
};

// Returns a NULL pointer, the SetupValue, and a Term for either a new
// symbolic variable or a term passed in.
// Should be called like:
//   variable "name" (llvm_int 32) SYM;
// or
//   variable "name" (llvm_int 32) (CON {{ 10 : [32] }});
let variable (type : LLVMType) (name : String) (v : { fresh : Term, t : Term}) = do {
  cfv <- if (eval_bool v.fresh) then (crucible_fresh_var name type) else variable_empty;
  let t = if (eval_bool v.fresh) then cfv else v.t;
  let s = from_cryptol t;
  return {p=crucible_null, s=s, t=t};
};

// Returns a newly-allocated pointer of the given type that points to
// a new symbolic variable or a term passed in. Also returns the
// SetupValue and Term for the variable.
let alloc (rw : Term) (type : LLVMType) (name : String) (v : { fresh : Term, t : Term}) = do {
  v' <- variable type name v;
  p <- pointer rw type;
  points_to p v'.s;
  return {p=p, s=v'.s, t=v'.t};
};

// Standard LLVM type names
let i8 = llvm_int 8;
let i16 = llvm_int 16;
let i32 = llvm_int 32;
let i64 = llvm_int 64;

// Standard C type names
let int8_t = llvm_int 8;
let int16_t = llvm_int 16;
let int32_t = llvm_int 32;
let int64_t = llvm_int 64;
let uint8_t = llvm_int 8;
let uint16_t = llvm_int 16;
let uint32_t = llvm_int 32;
let uint64_t = llvm_int 64;
let char = llvm_int 8;

// Provides the type of a struct. For example, 'struct_t "mystruct_t"';
let struct_t (name : String) = llvm_struct (str_concat "struct." name);

let struct (rw : Term) (struct_name : String) (s : [SetupValue]) = do {
  let s_struct = crucible_struct s;
  return {p=crucible_null, s=s_struct, t={{ error "struct Term field was accessed" }}};
};

// Provides a pointer, SetupValue, and Term associated with a string
// of size `size+1` that is NULL terminated. The Term (Cryptol value)
// DOES NOT have the NULL at the end of the string.
// This is to be used before the `execute` command.
let string_t (rw : Term) (size : Int) (string_name : String) = do {
  string <- variable (llvm_array size uint8_t) string_name FRESH;
  pstring <- pointer rw (llvm_array (eval_int {{ `size + 1 : [64] }}) uint8_t);
  let stringt = string.t;
  points_to pstring (from_cryptol {{ stringt # [0] }});
  return {p=pstring, s=string.s, t=string.t};
};

// Provides a pointer, SetupValue, and Term associated with a string
// of size `size+1` that is NULL terminated. The Term (Cryptol value)
// DOES have the NULL at the end of the string.
// This is to be used after the `execute` command.

let string_t' (size : Int) (string_name : String) = do {
  string <- alloc CONST (llvm_array (eval_int {{ `(size+1) : [64] }}) uint8_t) "string" FRESH;
  let s = string.t;
  postcond {{ s@(`size : [64]) == 0 }};
  return {p=string.p, s=from_cryptol string.t, t=string.t};
};

// This function will cause llvm_verify to error and, most
// importantly, provide a handy backtrace.
let error = do {
  crucible_precond {{ True }};
  crucible_postcond {{ True }};
  return {{ True }};
};

// This function is useful in the opposite case of an `if` statement
// with an `error`.
let noerror = do {
  return {{ True }};
};

// Provides an empty list of objects. Used as the basecase for
// `new_init_rec`.
let new_init_empty = do {
  return {s=[] : [SetupValue], bucket=[] : [b]} : CrucibleSetup {s : [SetupValue], bucket : [b]};
};

// Provides a list of initialized objects and a same-sized list of
// buckets where associated values can be stored.
rec new_init_rec (numElements : Int) (init_function : Int -> a -> CrucibleSetup {s : SetupValue, bucket : b}) (params : a) = do {
  err <- if (eval_bool {{ (`numElements : [32]) == 0 }}) then error else noerror; //numElements must be greater than 0
  element <- init_function (eval_int {{ (`numElements : [32]) - 1 }}) params;
  rest <- if (eval_bool {{ (`numElements : [32]) == 1 }}) then new_init_empty
          else new_init_rec (eval_int {{ (`numElements : [32]) - 1 }}) init_function params;

  let ret_s = (concat rest.s [element.s]);
  let ret_bucket = (concat rest.bucket [element.bucket]);

  return {s=ret_s : [SetupValue], bucket=ret_bucket : [b]};
};

// Provides an array of initialized objects and a same-sized list of
// buckets where associated values can be stored.
let new_init (rw : Term) (numElements : Int) (init_function : Int -> a -> CrucibleSetup {s : SetupValue, bucket : b}) (params : a) (type : LLVMType) = do {
  list <- new_init_rec numElements init_function params;
  let arr = array list.s;
  p <- pointer rw (llvm_array numElements type);
  points_to p arr;
  return {p=p : SetupValue, s=arr : SetupValue, bucket=list.bucket : [b]};
};
